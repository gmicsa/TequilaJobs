allprojects {
    group = 'ro.micsa'

    ext.springVersion='4.1.6.RELEASE'
    ext.hibernateVersion='4.3.9.Final'
    ext.mysqlConnectorVersion='5.1.35'
    ext.slf4jVersion='1.7.12'
    ext.junitVersion='4.12'
}

subprojects {
    apply plugin: 'java'
	apply plugin: 'maven-publish'
    apply from: "$rootDir/gradle/app/testjar.gradle"
    apply from: "$rootDir/gradle/app/integration-test.gradle"
    apply from: "$rootDir/gradle/app/acceptance-test.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "http://repository.jboss.org/nexus/content/groups/public/" }
    }

    publishing {
        repositories {
            maven {
                url "http://nexus.cegeka.be/nexus/content/repositories/cd-setup-releases/"
                credentials {
                    username = nexusUser
                    password = nexusPassword
                }
            }
        }
    }

    test {
        exclude 'ro/micsa/**/*TestBuilder*'
        testLogging {
            events "skipped", "failed"
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
        compile "log4j:log4j:1.2.17"
        compile "com.google.guava:guava:18.0"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.assertj:assertj-core:3.0.0"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    configurations {
        provided
    }

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
}